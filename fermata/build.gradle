android {
    defaultConfig {
        applicationId "me.aap.fermata"
        versionCode 40
        versionName "1.3.6"
        setProperty("archivesBaseName", "fermata-$versionName")
    }

    if (localProps['storeFile'] != null) {
        signingConfigs {
            release {
                storeFile file(localProps["storeFile"])
                storePassword localProps["storePassword"]
                keyAlias localProps["keyAlias"]
                keyPassword localProps["keyPassword"]
            }
        }
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), '../proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        debug {
            debuggable true
            applicationIdSuffix '.debug'
        }
    }

    flavorDimensions "version"

    productFlavors {
        mobile {
            dimension "version"
            resValue "string", "app_name", "@string/app_name_mobile"
        }

        auto {
            dimension "version"
            applicationIdSuffix '.auto'
            resValue "string", "app_name", "@string/app_name_auto"

            dependencies {
                implementation fileTree(dir: 'lib/auto', include: ['*.aar'])
            }
        }
    }

    if (findProject(':exoplayer') != null) dynamicFeatures = [':exoplayer', ':vlc']
    else dynamicFeatures = [':vlc']
}

dependencies {
    implementation project(':utils')
    implementation 'androidx.core:core:1.2.0'
    implementation 'androidx.media:media:1.1.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.2.0-alpha05'
    implementation "androidx.constraintlayout:constraintlayout:2.0.0-beta4"
    implementation 'com.google.android.play:core:1.6.5'
}