android {
    defaultConfig {
        applicationId "me.aap.fermata"
        versionCode 8
        versionName "1.1.0"
        setProperty("archivesBaseName", "fermata-$versionName")
    }

    def localPropsFile = file('../local.properties')

    if(localPropsFile.isFile()) {
        def localProps = new Properties()
        localProps.load(localPropsFile.newDataInputStream())

        if (localProps["storeFile"] != null) {
            signingConfigs {
                release {
                    storeFile file(localProps["storeFile"])
                    storePassword localProps["storePassword"]
                    keyAlias localProps["keyAlias"]
                    keyPassword localProps["keyPassword"]
                }
            }
        }
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), '../proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        debug {
            debuggable true
        }
    }

    flavorDimensions "version"

    productFlavors {
        mobile {
            dimension "version"
        }

        auto {
            dimension "version"

            dependencies {
                implementation fileTree(dir: 'lib/auto', include: ['*.aar'])
            }
        }
    }

    dynamicFeatures = [":exoplayer"]
}

dependencies {
    implementation 'androidx.core:core:1.1.0'
    implementation 'androidx.media:media:1.1.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.android.support:support-core-utils:28.0.0'
    implementation 'com.google.android.material:material:1.2.0-alpha03'
    implementation "androidx.constraintlayout:constraintlayout:2.0.0-beta4"
}