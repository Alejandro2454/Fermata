ext {
    VERSION_CODE = 55
    VERSION_NAME = "1.6.0"
    SDK_MIN_VERSION = 23
    SDK_TARGET_VERSION = 29
    SDK_COMPILE_VERSION = 29
    BUILD_TOOLS_VERSION = "29.0.2"
    ABI = project.properties['ABI'] ?: ['arm64-v8a', 'armeabi-v7a', 'x86']
    localProps = gradle.ext.localProps

    ANDROID_MATERIAL_VERSION = '1.2.0-alpha05'
    ANDROID_PLAY_CORE_VERSION = '1.7.2'
    ANDROIDX_CORE_VERSION = '1.2.0'
    ANDROIDX_MEDIA_VERSION = '1.1.0'
    ANDROIDX_APPCOMPAT_VERSION = '1.1.0'
    ANDROIDX_CONSTRAINTLAYOUT_VERSION = '2.0.0-beta4'
}

buildscript {

    repositories {
        google()
        jcenter()
        maven { url 'https://storage.googleapis.com/r8-releases/raw' }
    }

    dependencies {
        classpath 'com.android.tools:r8:2.0.64'
        classpath 'com.android.tools.build:gradle:4.0.0-beta03'
        classpath 'com.google.gms:google-services:4.3.3'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url "https://dl.bintray.com/videolan/Android" }
    }
}

subprojects {
    switch (name) {
        case 'fermata':
        case 'control':
            apply plugin: 'com.android.application'
            break
        case 'vlc':
        case 'exoplayer':
        case 'gdrive':
            apply plugin: 'com.android.dynamic-feature'
            break
        default:
            apply plugin: 'com.android.library'
            break
    }

    android {
        compileSdkVersion SDK_COMPILE_VERSION
        buildToolsVersion BUILD_TOOLS_VERSION

        defaultConfig {
            minSdkVersion SDK_MIN_VERSION
            targetSdkVersion SDK_TARGET_VERSION
            vectorDrawables.useSupportLibrary = true
            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
            resConfigs "en", "ru"
        }

        compileOptions {
            sourceCompatibility = JavaVersion.VERSION_1_8
            targetCompatibility = JavaVersion.VERSION_1_8
        }
    }

    dependencies {
        testImplementation 'junit:junit:4.12'
        androidTestImplementation 'androidx.test.ext:junit:1.1.1'
        androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
