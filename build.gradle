ext {
    VERSION_CODE = 49
    VERSION_NAME = "1.5.4"
    SDK_MIN_VERSION = 24
    SDK_TARGET_VERSION = 29
    SDK_COMPILE_VERSION = 29
    BUILD_TOOLS_VERSION = "29.0.2"
    ABI = project.properties['ABI'] ?: ['arm64-v8a', 'armeabi-v7a', 'x86']
    localProps = gradle.ext.localProps
}

buildscript {

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.0-beta03'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven {
            url "https://dl.bintray.com/videolan/Android"
        }
    }
}

subprojects {
    def dynFeature = false

    switch (name) {
        case 'fermata':
        case 'control':
            apply plugin: 'com.android.application'
            break
        case 'vlc':
        case 'exoplayer':
            apply plugin: 'com.android.dynamic-feature'
            dynFeature = true
            break
        default:
            apply plugin: 'com.android.library'
            break
    }

    android {
        compileSdkVersion SDK_COMPILE_VERSION
        buildToolsVersion BUILD_TOOLS_VERSION

        defaultConfig {
            minSdkVersion SDK_MIN_VERSION
            targetSdkVersion SDK_TARGET_VERSION
            vectorDrawables.useSupportLibrary = true
            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
            resConfigs "en", "ru"
            if(!dynFeature) multiDexEnabled true
        }

        compileOptions {
            coreLibraryDesugaringEnabled true
            sourceCompatibility = JavaVersion.VERSION_1_8
            targetCompatibility = JavaVersion.VERSION_1_8
        }
    }

    dependencies {
        testImplementation 'junit:junit:4.12'
        androidTestImplementation 'androidx.test.ext:junit:1.1.1'
        androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
        coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.0.5'
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
