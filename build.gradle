ext {
    SDK_MIN_VERSION = 23
    SDK_TARGET_VERSION = 29
    SDK_COMPILE_VERSION = 29
    BUILD_TOOLS_VERSION = "29.0.2"
    localProps = gradle.ext.localProps
}

buildscript {

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.6.0-rc01'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

subprojects {

    if (name == 'fermata') {
        apply plugin: 'com.android.application'
    } else if (name == 'exoplayer') {
        apply plugin: 'com.android.dynamic-feature'
    } else {
        apply plugin: 'com.android.library'
    }

    android {
        compileSdkVersion SDK_COMPILE_VERSION
        buildToolsVersion BUILD_TOOLS_VERSION

        defaultConfig {
            minSdkVersion SDK_MIN_VERSION
            targetSdkVersion SDK_TARGET_VERSION
            vectorDrawables.useSupportLibrary = true
            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        }

        compileOptions {
            sourceCompatibility = JavaVersion.VERSION_1_8
            targetCompatibility = JavaVersion.VERSION_1_8
        }
    }

    dependencies {
        testImplementation 'junit:junit:4.12'
        androidTestImplementation 'androidx.test.ext:junit:1.1.1'
        androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
